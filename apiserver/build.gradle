/*
 * This file is part of Bob.
 *
 * Bob is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Bob is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with Bob. If not, see <http://www.gnu.org/licenses/>.
 */

plugins {
    id "java"
    id "application"
    id "com.github.johnrengelman.shadow" version "6.0.0"
    id "com.github.ben-manes.versions" version "0.28.0"
}

group "org.bob-cd"
version "1.0-SNAPSHOT"

repositories {
    mavenCentral()
}

def vertx_version = "4.0.0-milestone5"

dependencies {
    implementation "io.vertx:vertx-core:$vertx_version"
    implementation "io.vertx:vertx-web:$vertx_version"
    implementation "io.vertx:vertx-web-client:$vertx_version"
    implementation "io.vertx:vertx-web-api-contract:$vertx_version"
    implementation "io.vertx:vertx-rabbitmq-client:$vertx_version"
    implementation "io.vertx:vertx-config:$vertx_version"
    implementation "org.slf4j:slf4j-simple:1.6.1"

    testImplementation "io.vertx:vertx-junit5:$vertx_version"
    testImplementation "org.junit.jupiter:junit-jupiter:5.6.2"
    testImplementation "org.assertj:assertj-core:3.8.0"
}

tasks.withType(JavaCompile) {
    options.compilerArgs += "--enable-preview"
}

tasks.withType(Test) {
    jvmArgs += "--enable-preview"
}

tasks.withType(JavaExec) {
    jvmArgs += "--enable-preview"
}

compileJava {
    sourceCompatibility = "14"
    targetCompatibility = "14"
}

test {
    systemProperty 'rabbitmq.port', 5673
    systemProperty 'crux.port', 7779
    useJUnitPlatform()
    testLogging {
        exceptionFormat = 'full'
        //showStandardStreams = true
    }
}

shadowJar {
    archivesBaseName = "bob-api-server"

    manifest {
        attributes "Main-Class": "Main"
    }
}

mainClassName = "Main"
